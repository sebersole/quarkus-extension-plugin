plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.20.0'

    // for local publishing (local plugin testing)
    id 'maven-publish'
}

group 'io.github.sebersole.quarkus'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation 'org.jboss:jandex:2.4.2.Final'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.14.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

var pluginKey = 'quarkusExtensionPlugin';
var pluginId = 'io.github.sebersole.quarkus-extension';

gradlePlugin {
    plugins {
        create( pluginKey ) {
            id = pluginId
            implementationClass = "io.github.sebersole.quarkus.QuarkusExtensionPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin"
    vcsUrl = "https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin"

    // sigh
    mavenCoordinates {
        groupId = project.group.toString()
        artifactId = project.name
        version = project.version.toString()
    }

    plugins.getByName( pluginKey ) {
        id = pluginId
        displayName = "Gradle plugin for building Quarkus extensions"
    }
}

test {
    useJUnitPlatform()
}
